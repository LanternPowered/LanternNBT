// Apply plugin
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

// Minimum version of Java required
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

// Basic project information
group = 'org.lanternpowered'
archivesBaseName = "lantern-${baseName}"

jar {
    classifier = ''
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    testCompile 'junit:junit:4.12'
}

processResources {
    from rootProject.file('LICENSE.txt')
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from rootProject.file('LICENSE.txt')
    from sourceSets.main.allSource
}
assemble.dependsOn sourceJar

license {
    header rootProject.file('HEADER.txt')
    sourceSets = project.sourceSets
    include '**/*.java'

    strictCheck true
    ignoreFailures = false

    ext {
        name = rootProject.name
        url = rootProject.url
        organization = rootProject.organization
    }

    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayKey') &&
        !version.endsWith("-SNAPSHOT")) {
    apply plugin: 'maven-publish'

    bintray {
        user = project.bintrayUser
        key = project.bintrayKey

        pkg {
            repo = 'maven'
            name = 'lanternnbt'
            userOrg = 'lanternpowered'
            licenses = [ 'MIT' ]
            vcsUrl = 'https://github.com/LanternPowered/LanternNBT.git'
            version {
                name = project.version
                released  = new Date()
                vcsTag = project.version
            }
        }

        publications = [ 'myPublication' ]
    }

    publishing {
        publications {
            myPublication(MavenPublication) {
                groupId project.group
                artifactId project.archivesBaseName
                version project.version
                artifact jar
                artifact sourceJar
            }
        }
    }
}